plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
    id 'jacoco'
}

group = 'de.felix'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}



dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT"
    implementation "com.zaxxer:HikariCP:6.0.0"

    testImplementation "org.junit.jupiter:junit-jupiter:5.11.0"
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation "org.mockito:mockito-junit-jupiter:5.14.2"
    testImplementation "com.h2database:h2:2.2.224"
    testImplementation "io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
    jvmArgs "-Dnet.bytebuddy.experimental=true"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand props
    }
}

tasks.register('copyJar', Copy) {
    from shadowJar
    into '/Users/felix/Documents/Lumeplay.net/ISkyWarsServer/plugins'
}

build.dependsOn copyJar
